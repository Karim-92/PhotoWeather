apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-parcelize'
apply plugin: 'kotlin-kapt'
apply plugin: 'dagger.hilt.android.plugin'
apply plugin: 'androidx.navigation.safeargs.kotlin'


def keysFile = rootProject.file("keys.properties")
def keyProperties = new Properties()
keyProperties.load(new FileInputStream(keysFile))

android {
    compileSdkVersion 30

    defaultConfig {
        applicationId "com.karim.photoweather"
        minSdkVersion 21
        targetSdkVersion 30
        versionCode 1
        versionName "1.0"

        buildConfigField("String", "API_KEY", keyProperties['API_KEY'])

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
        freeCompilerArgs += ["-Xopt-in=kotlin.RequiresOptIn"]
    }
    buildFeatures {
        dataBinding true
        viewBinding true
    }
    lintOptions {
        abortOnError false
    }
    testOptions {
        unitTests {
            includeAndroidResources = true
            returnDefaultValues = true
        }
    }
    sourceSets {
        androidTest.java.srcDirs += "src/test-common/java"
        test.java.srcDirs += "src/test-common/java"
        test.assets.srcDirs += files("$projectDir/schemas".toString())
    }
}

repositories {
    maven {
        url "https://jitpack.io"
    }
}


dependencies {

    // Network dependencies
    implementation 'com.squareup.moshi:moshi-kotlin:1.12.0'
    implementation 'com.squareup.retrofit2:retrofit:2.9.0'
    implementation 'com.squareup.retrofit2:converter-moshi:2.9.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:4.9.0'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    kapt 'com.squareup.moshi:moshi-kotlin-codegen:1.12.0'

    // Co-routines
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.5.0'
    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:1.5.0"
    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:1.5.0"


    // Glide for image loading
    implementation 'com.github.bumptech.glide:glide:4.12.0'
    kapt 'com.github.bumptech.glide:compiler:4.12.0'


    // Dagger hilt
    implementation 'com.google.dagger:hilt-android:2.37'
    kapt 'com.google.dagger:hilt-compiler:2.37'
    androidTestImplementation "com.google.dagger:hilt-android-testing:2.37"
    kaptAndroidTest "com.google.dagger:hilt-compiler:2.37"

    // debugging
    implementation "com.jakewharton.timber:timber:4.7.1"

    // Permissions Library
    implementation 'pub.devrel:easypermissions:3.0.0'

    // Lottie for animations
    implementation 'com.airbnb.android:lottie:3.7.0'

    // Skydoves libraries
    implementation "com.github.skydoves:baserecyclerviewadapter:1.0.4"
    implementation "com.github.skydoves:whatif:1.1.0"
    implementation ("com.github.skydoves:bindables:1.0.8") {
        exclude group: "com.google.android.material"
    }
    implementation 'com.github.skydoves:transformationlayout:1.0.9'
    implementation "com.github.skydoves:bundler:1.0.4"


    // Standard Libs
    implementation 'androidx.core:core-ktx:1.6.0'
    implementation 'androidx.appcompat:appcompat:1.3.0'
    implementation 'com.google.android.material:material:1.4.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    implementation "androidx.recyclerview:recyclerview:1.2.1"
    implementation 'androidx.lifecycle:lifecycle-livedata-ktx:2.3.1'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.3.1'
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'
    implementation 'androidx.room:room-runtime:2.3.0'
    implementation 'androidx.room:room-ktx:2.3.0'
    kapt 'androidx.room:room-compiler:2.3.0'
    implementation 'androidx.multidex:multidex:2.0.1'
    implementation "androidx.recyclerview:recyclerview:1.2.1"
    implementation 'androidx.preference:preference-ktx:1.1.1'
    implementation 'androidx.startup:startup-runtime:1.0.0'
    implementation "org.jetbrains.kotlin:kotlin-reflect:1.5.21"
    implementation 'com.github.dhaval2404:imagepicker:2.1'
    implementation 'com.google.android.gms:play-services-location:18.0.0'


    // Test Libraries
    testImplementation 'junit:junit:4.13.2'
    testImplementation "androidx.test:core:1.4.0"
    testImplementation "org.robolectric:robolectric:4.4"
    testImplementation "org.mockito.kotlin:mockito-kotlin:3.2.0"
    testImplementation "org.mockito:mockito-inline:3.5.13"
    testImplementation "androidx.arch.core:core-testing:2.1.0"
    testImplementation "com.squareup.okhttp3:mockwebserver:4.9.0"
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
    androidTestImplementation "com.google.truth:truth:1.0.1"
    androidTestImplementation "androidx.test.ext:junit:1.1.3"
    androidTestImplementation "com.android.support.test:runner:1.0.2"
}
kapt {
    correctErrorTypes true
}